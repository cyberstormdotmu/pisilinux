
# include global config
source ../_buildscripts/${current_repo}-${_arch}-cfg.conf

pkgname="kdepimlibs"
arch=('x86_64')
pkgver=4.14.5
pkgrel=1

pkgdesc="KDE Pim Libraries"
url="http://www.kde.org"
license=('GPL' 'LGPL' 'FDL')

install=${pkgname}.install
options=('docs' 'debug')
depends=('kdelibs' 'akonadi' 'libical' 'gpgme' 'prison' 'libdmtx' 'qrencode' 'qjson')
makedepends=('pkg-config' 'cmake' 'automoc4' 'boost' 'cyrus-sasl' 'openldap' 'prison' 'docbook-xsl')

groups=("kde" "kde-uninstall" "kde-minimal")

source=($_mirror/${pkgname}-${pkgver}.tar.xz)
md5sums=('0c027a427a15487d594aa1d6d20b339c')

build() {
	msg "starting build ..."
	cd ${srcdir}
	mkdir -p build
	cd build

	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=${_build_type} \
		-DCMAKE_INSTALL_PREFIX=${_installprefix} \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed'

	make
}

package() {
	cd ${srcdir}/build
	
	make DESTDIR=${pkgdir} install
}
