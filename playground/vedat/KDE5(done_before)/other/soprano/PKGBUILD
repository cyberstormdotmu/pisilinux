
pkgname=soprano
pkgver=2.9.4
pkgrel=1
pkgdesc='A library which provides a highly usable object-oriented C++/Qt4 framework for RDF data'
arch=('x86_64')
url='http://soprano.sourceforge.net/'
license=('GPL' 'LGPL')
depends=('qt' 'redland-storage-virtuoso' 'libiodbc' 'virtuoso>=6.1.3' 'clucene')
makedepends=('cmake' 'openjdk' 'doxygen')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('91413e5783efeee0beb747ea4c5c9ef0')

build() {
  cd "${srcdir}"
  
  rm -rf build
  mkdir build
  cd build
  # we need the rpath
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr 
  make
}

package() {
  cd "${srcdir}"/build
  
  make DESTDIR="${pkgdir}" install
}
